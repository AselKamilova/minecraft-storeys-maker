plugins {
    id "com.moowork.node" version "1.2.0"
}

project.afterEvaluate {
    tasks.npm_install.outputs.dir(file('node_modules'))
}

node {
    // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md#configuring-the-plugin
    version = '8.11.1'
    npmVersion = '6.0.1'
    download = true
}

task npmInstall(overwrite: true, type: NpmTask, dependsOn: 'npmSetup') {
    inputs.file('package.json')
    outputs.upToDateWhen { file('node_modules').exists() }

    npmCommand = ['install']
}

task npmBuildProd(type: NpmTask) {
    inputs.files(fileTree('src'))
    inputs.file('package.json')
    inputs.file('webpack.config.js')

    outputs.dir('dist')

    dependsOn npmInstall
    args = ['run', 'build']
}

task install {
    dependsOn npmBuildProd
}

assemble.dependsOn(install)

apply plugin: 'eclipse'

dependencies {
    // just for mapping our enums to catalog types,
    // but this is an implementation detail; users of this API won't directly use spongeapi themselves
    // TODO remove this again, and move enum mapping into impl instead of API...
    implementation 'org.spongepowered:spongeapi:7.0.0'

    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"

    compileOnly "io.vertx:vertx-codegen:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"
    compileOnly "io.vertx:vertx-service-proxy:$vertxVersion:processor"

    testCompile "io.vertx:vertx-unit:$vertxVersion"
}

// hack work-around for https://github.com/vert-x3/vertx-lang-js/issues/76 ;)
task finalize << {
    ant.replace(file: "build/classes/java/main/Minecraft-ts/minecraft-proxy.ts", token: "Minecraft-ts/", value: "./")
    println('finally!')
}
build.finalizedBy(finalize)

eclipse {
    classpath {
        file.beforeMerged { cp ->
            cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/classes/java/main', null) )
        }
    }
}
